import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.6.21'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.6.21'

    id 'org.openapi.generator' version '6.2.1'
}

group = 'net.stepniak.morenomodels.service'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
targetCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'commons-io:commons-io:2.11.0'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.7'
    implementation 'io.swagger.core.v3:swagger-models:2.2.6'
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '11'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


openApiGenerate {
    generatorName.set("kotlin-spring")
    inputSpec.set("${project(':model').projectDir}/src/main/resources/moreno-models.yaml")
    outputDir.set("$buildDir/generated")
    packageName.set("net.stepniak.morenomodels.service.generated")
    apiPackage.set("net.stepniak.morenomodels.service.generated")
    modelPackage.set("net.stepniak.morenomodels.service.generated.model")

    configOptions = [
            interfaceOnly: "true"
    ]
}

java.sourceSets["main"].java.srcDir("$buildDir/generated/src/main/kotlin")
